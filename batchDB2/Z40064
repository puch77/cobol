       IDENTIFICATION DIVISION.
@@@@@  PROGRAM-ID. Z40064.
       AUTHOR. MONIKA PUCHALSKA.
       DATE-WRITTEN.  04/12/2024.
       DATE-COMPILED. 04/12/2024.

      ******************************************************************
      *                         --------
@@@@@ *                         Z40064
      *                         --------
      *   PROGRAM THAT SELECTS 20 RECORDS FROM TABLE IBMUSER.CLIENTS,
      *   USING ROWSET-POSITIONED SURSOR.
      *
      *   DB2 TABLES             ACCESS
      *   ----------             ------
      *    CLIENTS               SELECT
      *
      *

      *****************************************************************
      *              ENVIRONMENT DIVISION
      ******************************************************************
      *
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
      *
       SOURCE-COMPUTER.
           IBM-3090.
       OBJECT-COMPUTER.
           IBM-3090.
       SPECIAL-NAMES.

      ******************************************************************
      *                  DATA DIVISION                                 *
      ******************************************************************
       DATA DIVISION.


      ******************************************************************
      *                  WORKING-STORAGE SECTION                       *
      ******************************************************************
       WORKING-STORAGE SECTION.


      *********************
      *     SWITCHES      *
      *********************

       01 SW.

      *    SWITCH INDICATING IF CURSOR HAS MORE DATA
           05 SW-CCLI-MORE-DATA                    PIC X(1) VALUE 'N'.
                88 SO-CCLI-MORE-DATA-YES           VALUE 'Y'.
                88 SO-CCLI-MORE-DATA-NO            VALUE 'N'.

      *********************
      *     CONSTANTS     *
      *********************
       01 CT.
          02 CT-PROGRAM-NAME                  PIC X(8) VALUE 'Z40054'.
          02 CT-SQLCODES.
             05 CT-SQLCODE-OK                 PIC S9(5) VALUE 0.
             05 CT-SQLCODE-END-OF-DATA        PIC S9(5) VALUE 100.
          02 CT-SET-SIZE                      PIC S9(4) COMP VALUE 20.
      *********************
      *     VARIABLES     *
      *********************

       01 WS.
      *    VISIBLE ERROR CODES
           05 WS-SQLCODE-READABLE             PIC -999.
           05 WS-COUNT                        PIC S9(09) COMP VALUE +0.
           05 WS-COUNT-READABLE               PIC -9(9).
           05 WS-STATEMENT-NR                 PIC 99 VALUE 0.
           05 WS-INDEX                        PIC 9(2) VALUE 0.
           05 WS-SIZE                         PIC 9(2) VALUE 20.
           05 CL-NUMBER      PIC S9(9) COMP OCCURS 20 TIMES.
           05 CL-FIRST-NAME  PIC X(30) OCCURS 20 TIMES.
           05 CL-LAST-NAME   PIC X(30) OCCURS 20 TIMES.
      *********************
      *     COPYBOOKS     *
      *********************
      *********************
      *   DB2 DCLGENS     *
      *********************

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

      *    CLIENTS - TABLE WITH CLIENTS OF IBMUSER
           EXEC SQL
              INCLUDE CLIENTS
           END-EXEC.
      *********************
      *   DB2 CURSORS     *
      *********************
      * CCLI CURSOR IS FOR FETCHING MULTIPLY ROWS (ROWSET) FROM THE
      * TABLE CLIENTS, IN A SINGLE OPERATION
           EXEC SQL
             DECLARE CCLI CURSOR WITH ROWSET POSITIONING FOR
             SELECT CLI_NUMBER,
                    CLI_FIRST_NAME,
                    CLI_LAST_NAME
             FROM IBMUSER.CLIENTS
             ORDER BY CLI_NUMBER
           END-EXEC.
      *
      ******************************************************************
      *                                                                *
      *                PROCEDURE DIVISION                              *
      *                                                                *
      ******************************************************************
       PROCEDURE DIVISION.
      *
           PERFORM  1000-INIT
      *
           PERFORM  2000-PROCESS
      *
           PERFORM  3000-FINAL.
      *

      /
      ******************************************************************
      *                    1000-INIT                                   *
      *                                                                *
      ******************************************************************
       1000-INIT.
      *
           DISPLAY '1000-INIT'
           .
      /
      ******************************************************************
      *                                                                *
      *                     2000-PROCESS                               *
      *                                                                *
      ******************************************************************
      *
       2000-PROCESS.
           DISPLAY '2000-PROCESS'
      * GET RECORDS FROM TABLE CLIENTS USING A CURSOR WITH ROWSET
      * POSITIONING
           PERFORM 7010-OPEN-CCLI
           PERFORM 7020-FETCH-CCLI

           PERFORM UNTIL SO-CCLI-MORE-DATA-NO
      * DISPLAY A ROWSET OF 20 RECORDS, FOLLOWED BY AN EMPTY LINE
               PERFORM 2100-DISPLAY-ROWSET
               DISPLAY ' '
               PERFORM 7020-FETCH-CCLI
           END-PERFORM
      * CHECK THE NUMBER OF ROWS IN THE LAST FETCH WITH END OF DATA
      * IF STILL SOME DATA LEFT, DISPLAY THEM
           MOVE SQLERRD(3) TO WS-SIZE
           IF WS-SIZE > 0
             PERFORM 2100-DISPLAY-ROWSET
           END-IF

           PERFORM 7030-CLOSE-CCLI
           .
      ******************************************************************
      *                                                                *
      *                2100-DISPLAY-ROWSET                            *
      *                                                                *
      ******************************************************************
      * DISPLAY RECORDS IN GROUPS OF 20 OR IN THE LAST ITERATION
      * TO THE END OF THE RECORDS (WS-SIZE)
       2100-DISPLAY-ROWSET.
           PERFORM VARYING WS-INDEX FROM 1 BY 1
             UNTIL WS-INDEX > WS-SIZE
                DISPLAY CL-NUMBER(WS-INDEX) '  '
                        CL-FIRST-NAME(WS-INDEX) ' '
                        CL-LAST-NAME(WS-INDEX)
           END-PERFORM
           .
      ******************************************************************
      *                    7010-OPEN-CCLI                              *
      *                                                                *
      ******************************************************************
       7010-OPEN-CCLI.

           EXEC SQL
             OPEN CCLI
           END-EXEC
      *
           IF SQLCODE = CT-SQLCODE-OK
              SET SO-CCLI-MORE-DATA-YES TO TRUE
           ELSE
              MOVE 3 TO WS-STATEMENT-NR
              PERFORM 9000-DB2-ERROR-HANDLING
           END-IF
           .
      ******************************************************************
      *                    7020-FETCH-CCLI                             *
      *                                                                *
      ******************************************************************
       7020-FETCH-CCLI.

           EXEC SQL
             FETCH NEXT ROWSET FROM CCLI
             FOR  :CT-SET-SIZE ROWS
             INTO :CL-NUMBER,
                  :CL-FIRST-NAME,
                  :CL-LAST-NAME
           END-EXEC.
      *
           EVALUATE SQLCODE
             WHEN CT-SQLCODE-OK
               CONTINUE
             WHEN CT-SQLCODE-END-OF-DATA
               SET SO-CCLI-MORE-DATA-NO TO TRUE
             WHEN OTHER
               MOVE 4                       TO WS-STATEMENT-NR
               PERFORM 9000-DB2-ERROR-HANDLING
           END-EVALUATE
           .
      ******************************************************************
      *                    7030-CLOSE-CCLI
      *                                                                *
      ******************************************************************
       7030-CLOSE-CCLI.

           EXEC SQL
             CLOSE CCLI
           END-EXEC
      *
           IF SQLCODE = CT-SQLCODE-OK
              CONTINUE
           ELSE
              MOVE 5 TO WS-STATEMENT-NR
              PERFORM 9000-DB2-ERROR-HANDLING
           END-IF
           .
      ******************************************************************
      *                                                                *
      *                       3000-FINAL                               *
      *                                                                *
      *                     SUCCESSFUL END                             *
      ******************************************************************
      *
       3000-FINAL.
      *
           DISPLAY '3000-FINAL'
           STOP RUN
           .
      ******************************************************************
      *                                                                *
      *                    9000-DB2-ERROR-HANDLING                     *
      *                                                                *
      ******************************************************************
       9000-DB2-ERROR-HANDLING.
           MOVE SQLCODE          TO WS-SQLCODE-READABLE
           DISPLAY 'SQL ERROR IN  ' CT-PROGRAM-NAME
           DISPLAY 'SQLCODE:      ' WS-SQLCODE-READABLE
           DISPLAY 'SQLERRMC:     ' SQLERRMC
           DISPLAY 'STATEMENT NR: ' WS-STATEMENT-NR
      *    PERFORM 9001-DB2-ROLLBACK
           MOVE 12               TO RETURN-CODE
           PERFORM 3000-FINAL
           .
      ******************************************************************
      *                                                                *
      *                    9001-DB2-ROLLBACK                           *
      *                                                                *
      ******************************************************************
       9001-DB2-ROLLBACK.
           EXEC SQL
             ROLLBACK
           END-EXEC
           .
